From 693662eba83209dbf23621a9b38b6511a2e6a64c Mon Sep 17 00:00:00 2001
From: pchome <pchome@pchome.local>
Date: Fri, 9 Nov 2018 16:11:05 +0200
Subject: [PATCH] Initial meson build system support

---
 meson.build       | 20 ++++++++++++++++++++
 meson_options.txt |  4 ++++
 src/meson.build   | 43 +++++++++++++++++++++++++++++++++++++++++++
 3 files changed, 67 insertions(+)
 create mode 100644 meson.build
 create mode 100644 meson_options.txt
 create mode 100644 src/meson.build

diff --git a/meson.build b/meson.build
new file mode 100644
index 0000000..890e81a
--- /dev/null
+++ b/meson.build
@@ -0,0 +1,33 @@
+project('FAudio', ['c'])
+
+cc = meson.get_compiler('c')
+
+sdl_dep = dependency('sdl2', version : '>= 2.0.9', method : 'pkg-config')
+
+# Check FFmpeg
+ffmpeg_dep = []
+if get_option('enable_ffmpeg')
+  add_project_arguments('-DHAVE_FFMPEG=1', language : 'c')
+  ffmpeg_dep = [
+    dependency('libavcodec', version : '>= 50', method : 'pkg-config'),
+    dependency('libavutil',  version : '>= 50', method : 'pkg-config'),
+  ]
+endif
+
+faudio_deps = [ sdl_dep, ffmpeg_dep ]
+
+if get_option('buildtype') != 'debug'
+  add_project_arguments('-DFAUDIO_RELEASE=1', language : 'c')
+endif
+
+subdir('src')
+
+pkg = import('pkgconfig')
+pkg.generate(
+  libraries : faudio_lib,
+  subdirs : ['.'],
+  version : '1.0',
+  name : 'libFAudio',
+  filebase : 'FAudio',
+  description : 'FAudio library.')
+
diff --git a/meson_options.txt b/meson_options.txt
new file mode 100644
index 0000000..9d8b5cb
--- /dev/null
+++ b/meson_options.txt
@@ -0,0 +1,4 @@
+option('enable_ffmpeg', type : 'boolean', value : true,
+  description: 'Support for decoding xWMA resources using FFmpeg')
+option('enable_xnasong', type : 'boolean', value : true,
+  description: 'Support for XNASong')
diff --git a/src/meson.build b/src/meson.build
new file mode 100644
index 0000000..8db2df5
--- /dev/null
+++ b/src/meson.build
@@ -0,0 +1,43 @@
+faudio_src = [
+  'F3DAudio.c',
+  'FAudio.c',
+  'FAudio_internal.c',
+  'FAudio_internal_simd.c',
+  'FAudioFX_reverb.c',
+  'FAudioFX_volumemeter.c',
+  'FACT.c',
+  'FACT3D.c',
+  'FACT_internal.c',
+  'FAPOBase.c',
+  'FAPOFX.c',
+  'FAPOFX_eq.c',
+  'FAPOFX_masteringlimiter.c',
+  'FAPOFX_reverb.c',
+  'FAPOFX_echo.c',
+  'FAudio_platform_sdl2.c',
+]
+
+if get_option('enable_xnasong')
+  faudio_src += 'XNA_Song.c'
+endif
+
+if get_option('enable_ffmpeg')
+  faudio_src += 'FAudio_ffmpeg.c'
+endif
+
+faudio_headers = [
+  'FAudio.h',
+  'FAudioFX.h',
+  'F3DAudio.h',
+  'FAPOFX.h',
+  'FAPO.h',
+  'FAPOBase.h',
+  'FACT.h',
+  'FACT3D.h',
+]
+
+faudio_lib = shared_library('FAudio', faudio_src,
+  dependencies : faudio_deps,
+  install : true)
+
+install_headers(faudio_headers)
-- 
2.19.1

